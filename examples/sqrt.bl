/**
  * sqrt.bl -- Square root function calculated using Heron's method
  */


include 'std/math.bl';  /* include sqrt from library to see how good my sqrt
                           can get */

fun sqrt(x, x0, iterations) {
    res = x0;
    for (i = 0; i < iterations; i += 1) {
        res = (res + x / res) / 2;
    }
    return res;
}

fun sqrt_init(x) {
    n = Math.frexp(x)[1];
    x = Math.frexp(x)[0];
    return Math.ldexp(1 + x, (n >> 1) - 1);
}

x = float(input('enter number: '));

print('my sqrt vs Math.sqrt');

bad_x0 = 1;  # bad initial guess
good_x0 = sqrt_init(x);  # good initial guess

print('bad initial guess:');
for (i = 1; i <= 6; i += 1) {
    print(
          'iterations = ' + str(i) + '\t'
        + str(sqrt(x, bad_x0, i)) + '\t'
        + str(Math.sqrt(x))
    );
}

print();
print('good initial guess:');
for (i = 1; i <= 6; i += 1) {
    print(
          'iterations = ' + str(i) + '\t'
        + str(sqrt(x, good_x0, i)) + '\t'
        + str(Math.sqrt(x))
    );
}
